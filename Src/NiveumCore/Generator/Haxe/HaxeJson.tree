$Comment
    ==========================================================================

      File:        HaxeJson.tree
      Location:    Niveum.Core <Tree>
      Description: 对象类型结构Haxe JSON通讯模板
      Version:     2019.04.28.
      Copyright(C) F.R.C.

    ==========================================================================

#Option
    EnableEmbeddedExpr True

#Namespace Niveum.ObjectSchema.HaxeJson

#Import
    System
    System.Collections.Generic
    System.Linq
    Firefly

#Template IJsonSender
    interface IJsonSender
    {
        function send(commandName : String, commandHash : String, parameters : String) : Void;
    }

#Template JsonSerializationClient Hash:UInt64 Commands:List<TypeDef> SchemaClosureGenerator:ISchemaClosureGenerator NamespaceName:String
    private class ApplicationClient implements IApplicationClient
    {
        public var s : IJsonSender;
        public var clientCommandCallbacks : Map<String, Array<{commandHash : String, _callback : String -> Void}>>;

        public function new()
        {
        }

        public var hash(get, null) : String;
        public function get_hash() : String
        {
            return "${Hash.ToString("X16", System.Globalization.CultureInfo.InvariantCulture)}";
        }

        public function dequeueCallback(commandName : String) : Void
        {
            clientCommandCallbacks.get(commandName).shift();
        }

        private function addCallback(commandName : String, commandHash : String, _callback : String -> Void) : Void
        {
            if (clientCommandCallbacks.exists(commandName))
            {
                clientCommandCallbacks.get(commandName).push({commandHash : commandHash, _callback : _callback});
            }
            else
            {
                var q = new Array<{commandHash : String, _callback : String -> Void}>();
                q.push({commandHash : commandHash, _callback : _callback});
                clientCommandCallbacks.set(commandName, q);
            }
        }

        $$
            foreach (var c in Commands)
            {
                if (c.OnClientCommand)
                {
                    var CommandNameString = GetEscapedStringLiteral(c.ClientCommand.FullName());
                    var RequestTypeString = GetSuffixedTypeString(c.ClientCommand.Name, c.ClientCommand.Version, "Request", NamespaceName);
                    var ReplyTypeString = GetSuffixedTypeString(c.ClientCommand.Name, c.ClientCommand.Version, "Reply", NamespaceName);
                    var RequestName = GetSuffixedTypeName(c.ClientCommand.Name, c.ClientCommand.Version, "Request", NamespaceName);
                    var ReplyName = GetSuffixedTypeName(c.ClientCommand.Name, c.ClientCommand.Version, "Reply", NamespaceName);
                    var Name = c.ClientCommand.GetTypeSpec().SimpleName(NamespaceName);
                    var CommandHash = ((UInt32)(SchemaClosureGenerator.GetSubSchema(new List<TypeDef> { c }, new List<TypeSpec> { }).GetNonversioned().GetNonattributed().Hash().Bits(31, 0))).ToString("X8", System.Globalization.CultureInfo.InvariantCulture);
                    ##
                        public function [[${LowercaseCamelize(Name)}]](r : ${RequestTypeString}, _callback : ${ReplyTypeString} -> Void) : Void
                        {
                            var request = Json.stringify(JsonTranslator.[[${LowercaseCamelize(RequestName)}ToJson]](r));
                            addCallback(${CommandNameString}, "${CommandHash}", function(parameters) { return _callback(JsonTranslator.[[${LowercaseCamelize(ReplyName)}FromJson]](Json.parse(parameters))); });
                            s.send(${CommandNameString}, "${CommandHash}", request);
                        }
                }
                else if (c.OnServerCommand)
                {
                    var Name = c.ServerCommand.GetTypeSpec().SimpleName(NamespaceName);
                    var EventTypeString = GetSuffixedTypeString(c.ServerCommand.Name, c.ServerCommand.Version, "Event", NamespaceName);
                    ##
                        public var [[${LowercaseCamelize(Name)}]] : ${EventTypeString} -> Void;
                        public function [[raise${Name}]](e : ${EventTypeString}) : Void { if ([[${LowercaseCamelize(Name)}]] != null) { [[${LowercaseCamelize(Name)}]](e); } }
                }
            }
    }

    @:final
    class JsonSerializationClient
    {
        public function getApplicationClient() : IApplicationClient
        {
            return c;
        }

        private var c : ApplicationClient;
        private var serverCommands : Map<String, {commandHash : String, _callback : String -> Void}>;

        public function new(s : IJsonSender)
        {
            c = new ApplicationClient();
            c.s = s;
            c.clientCommandCallbacks = new Map<String, Array<{commandHash : String, _callback : String -> Void}>>();
            serverCommands = new Map<String, {commandHash : String, _callback : String -> Void}>();
            $$
                foreach (var c in Commands)
                {
                    if (c.OnServerCommand)
                    {
                        var CommandNameString = GetEscapedStringLiteral(c.ServerCommand.FullName());
                        var EventTypeString = GetSuffixedTypeString(c.ServerCommand.Name, c.ServerCommand.Version, "Event", NamespaceName);
                        var EventName = GetSuffixedTypeName(c.ServerCommand.Name, c.ServerCommand.Version, "Event", NamespaceName);
                        var Name = c.ServerCommand.GetTypeSpec().SimpleName(NamespaceName);
                        var CommandHash = ((UInt32)(SchemaClosureGenerator.GetSubSchema(new List<TypeDef> { c }, new List<TypeSpec> { }).GetNonversioned().GetNonattributed().Hash().Bits(31, 0))).ToString("X8", System.Globalization.CultureInfo.InvariantCulture);
                        ##
                            serverCommands.set(${CommandNameString}, {commandHash : "${CommandHash}", _callback : function(parameters) { c.[[raise${Name}]](JsonTranslator.[[${LowercaseCamelize(EventName)}FromJson]](Json.parse(parameters))); }});
                    }
                }
        }

        public function handleResult(commandName : String, commandHash : String, parameters : String) : Void
        {
            if (c.clientCommandCallbacks.exists(commandName))
            {
                var q = c.clientCommandCallbacks.get(commandName);
                if (q.length == 0)
                {
                    throw "InvalidOperationException: " + commandName + "@" + commandHash;
                }
                var callbackPair = q[0];
                if (callbackPair.commandHash != commandHash)
                {
                    throw "InvalidOperationException: " + commandName + "@" + commandHash;
                }
                q.shift();
                var _callback = callbackPair._callback;
                _callback(parameters);
                return;
            }

            if (serverCommands.exists(commandName))
            {
                var callbackPair = serverCommands.get(commandName);
                if (callbackPair.commandHash != commandHash)
                {
                    throw "InvalidOperationException: " + commandName + "@" + commandHash;
                }
                var _callback = callbackPair._callback;
                _callback(parameters);
                return;
            }

            throw "InvalidOperationException: " + commandName + "@" + commandHash;
        }
    }

#Template JsonTranslator Schema:Schema NamespaceName:String
    @:final
    class JsonTranslator /* static */
    {
        private static function hasField(o : Dynamic, field : String) : Boolean
        {
            return Reflect.hasField(o, field);
        }
        private static function getField(o : Dynamic, field : String) : Dynamic
        {
            if (!Reflect.hasField(o, field)) { throw "InvalidOperation"; }
            return Reflect.field(o, field);
        }
        private static function setField(o : Dynamic, field : String, value : Dynamic) : Void
        {
            Reflect.setField(o, field, value);
        }

        ${GetJsonTranslatorSerializers(Schema, NamespaceName)}
    }

#Template JsonTranslator_Primitive_Unit
    public static function unitFromJson(j : Dynamic) : Unit
    {
        return {};
    }
    public static function unitToJson(v : Unit) : Dynamic
    {
        return {};
    }

#Template JsonTranslator_Primitive_Boolean
    public static function booleanFromJson(j : Dynamic) : Boolean
    {
        return cast(j, Boolean);
    }
    public static function booleanToJson(v : Boolean) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_String
    public static function stringFromJson(j : Dynamic) : String
    {
        return cast(j, String);
    }
    public static function stringToJson(v : String) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Int
    public static function intFromJson(j : Dynamic) : Int
    {
        return cast(j, Int);
    }
    public static function intToJson(v : Int)
    {
        return v;
    }

#Template JsonTranslator_Primitive_Real
    public static function realFromJson(j : Dynamic) : Real
    {
        return cast(j, Real);
    }
    public static function realToJson(v : Real) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Byte
    public static function byteFromJson(j : Dynamic) : Byte
    {
        return cast(j, Byte);
    }
    public static function byteToJson(v : Byte) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_UInt8
    public static function uint8FromJson(j : Dynamic) : UInt8
    {
        return cast(j, UInt8);
    }
    public static function uint8ToJson(v : UInt8) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_UInt16
    public static function uint16FromJson(j : Dynamic) : UInt16
    {
        return cast(j, UInt16);
    }
    public static function uint16ToJson(v : UInt16) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_UInt32
    public static function uint32FromJson(j : Dynamic) : UInt32
    {
        return cast(j, UInt32);
    }
    public static function uint32ToJson(v : UInt32) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_UInt64
    public static function uint64FromJson(j : Dynamic) : UInt64
    {
        return cast(int64FromJson(j), UInt64);
    }
    public static function uint64ToJson(v : UInt64) : Dynamic
    {
        return int64ToJson(cast(v, Int64));
    }

#Template JsonTranslator_Primitive_Int8
    public static function int8FromJson(j : Dynamic) : Int8
    {
        return cast(j, Int8);
    }
    public static function int8ToJson(v : Int8) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Int16
    public static function int16FromJson(j : Dynamic) : Int16
    {
        return cast(j, Int16);
    }
    public static function int16ToJson(v : Int16) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Int32
    public static function int32FromJson(j : Dynamic) : Int32
    {
        return cast(j, Int32);
    }
    public static function int32ToJson(v : Int32) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Int64
    public static function int64FromJson(j : Dynamic) : Int64
    {
        return Int64.fromFloat(cast(j, Float));
    }
    public static function int64ToJson(v : Int64) : Dynamic
    {
        return v.high * 4294967296.0 + v.low;
    }

#Template JsonTranslator_Primitive_Float32
    public static function float32FromJson(j : Dynamic) : Float32
    {
        return cast(j, Float32);
    }
    public static function float32ToJson(v : Float32) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Float64
    public static function float64FromJson(j : Dynamic) : Float64
    {
        return cast(j, Float64);
    }
    public static function float64ToJson(v : Float64) : Dynamic
    {
        return v;
    }

#Template JsonTranslator_Primitive_Type
    public static function typeFromJson(j : Dynamic) : Void
    {
        throw "NotSupportedException";
    }
    public static function typeToJson(v : Dynamic) : Void
    {
        throw "NotSupportedException";
    }

#Template JsonTranslator_Alias a:AliasDef NamespaceName:String
    $$
        var TypeString = GetTypeString(a.GetTypeSpec(), NamespaceName);
        var Name = a.GetTypeSpec().SimpleName(NamespaceName);
        var ValueSimpleName = a.Type.SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(Name)}FromJson]](j : Dynamic) : ${TypeString}
    {
        return [[${LowercaseCamelize(ValueSimpleName)}FromJson]](j);
    }
    public static function [[${LowercaseCamelize(Name)}ToJson]](o : ${TypeString}) : Dynamic
    {
        return [[${LowercaseCamelize(ValueSimpleName)}ToJson]](o);
    }

#Template JsonTranslator_Record r:RecordDef NamespaceName:String
    ${JsonTranslator_Record(r.GetTypeSpec().SimpleName(NamespaceName), GetTypeString(r.GetTypeSpec(), NamespaceName), r.Fields, NamespaceName)}

#Template JsonTranslator_Record Name:String TypeString:String Fields:List<VariableDef> NamespaceName:String
    public static function [[${LowercaseCamelize(Name)}FromJson]](j : Dynamic) : ${TypeString}
    {
        return
        {
            $$
                foreach (var a in Fields)
                {
                    ##
                        [[${LowercaseCamelize(a.Name)}]] : [[${LowercaseCamelize(a.Type.SimpleName(NamespaceName))}FromJson]](getField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))})),
                }
        };
    }
    public static function [[${LowercaseCamelize(Name)}ToJson]](o : ${TypeString}) : Dynamic
    {
        var j : Dynamic = {};
        $$
            foreach (var a in Fields)
            {
                ##
                    setField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}, [[${LowercaseCamelize(a.Type.SimpleName(NamespaceName))}ToJson]](o.[[${LowercaseCamelize(a.Name)}]]));
            }
        return j;
    }

#Template JsonTranslator_TaggedUnion tu:TaggedUnionDef NamespaceName:String
    ${JsonTranslator_TaggedUnion(tu.GetTypeSpec().SimpleName(NamespaceName), GetTypeString(tu.GetTypeSpec(), NamespaceName), tu.Alternatives, NamespaceName)}

#Template JsonTranslator_TaggedUnion Name:String TypeString:String Alternatives:List<VariableDef> NamespaceName:String
    public static function [[${LowercaseCamelize(Name)}FromJson]](j : Dynamic) : ${TypeString}
    {
        $$
            foreach (var a in Alternatives)
            {
                if (a.Type.OnTypeRef && a.Type.TypeRef.NameMatches("Unit"))
                {
                    ##
                        if (hasField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}))
                        {
                            return ${TypeString}.[[${LowercaseCamelize(a.Name)}]];
                        }
                }
                else
                {
                    ##
                        if (hasField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}))
                        {
                            var v = [[${LowercaseCamelize(a.Type.SimpleName(NamespaceName))}FromJson]](getField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}));
                            return ${TypeString}.[[${LowercaseCamelize(a.Name)}]](v);
                        }
                }
            }
        throw "InvalidOperation";
    }
    public static function [[${LowercaseCamelize(Name)}ToJson]](o : ${TypeString}) : Dynamic
    {
        var j : Dynamic = {};
        switch (o)
        {
        $$
            foreach (var a in Alternatives)
            {
                if (a.Type.OnTypeRef && a.Type.TypeRef.NameMatches("Unit"))
                {
                    ##
                        case [[${LowercaseCamelize(a.Name)}]]:
                            setField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}, unitToJson({}));
                }
                else
                {
                    ##
                        case [[${LowercaseCamelize(a.Name)}]](v):
                            setField(j, ${GetEscapedStringLiteral(LowercaseCamelize(a.Name))}, [[${LowercaseCamelize(a.Type.SimpleName(NamespaceName))}ToJson]](v));
                }
            }
        }
        return j;
    }

#Template JsonTranslator_Enum e:EnumDef NamespaceName:String
    $$
        var Name = e.GetTypeSpec().SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(Name)}FromJson]](j : Dynamic) : ${GetTypeString(e.UnderlyingType, NamespaceName)}
    {
        return cast(j, ${GetTypeString(e.UnderlyingType, NamespaceName)});
    }
    public static function [[${LowercaseCamelize(Name)}ToJson]](o : ${GetTypeString(e.UnderlyingType, NamespaceName)}) : Dynamic
    {
        return o;
    }

#Template JsonTranslator_ClientCommand c:ClientCommandDef NamespaceName:String
    ${JsonTranslator_Record(GetSuffixedTypeName(c.Name, c.Version, "Request", NamespaceName), GetSuffixedTypeString(c.Name, c.Version, "Request", NamespaceName), c.OutParameters, NamespaceName)}
    ${JsonTranslator_TaggedUnion(GetSuffixedTypeName(c.Name, c.Version, "Reply", NamespaceName), GetSuffixedTypeString(c.Name, c.Version, "Reply", NamespaceName), c.InParameters, NamespaceName)}

#Template JsonTranslator_ServerCommand c:ServerCommandDef NamespaceName:String
    ${JsonTranslator_Record(GetSuffixedTypeName(c.Name, c.Version, "Event", NamespaceName), GetSuffixedTypeString(c.Name, c.Version, "Event", NamespaceName), c.OutParameters, NamespaceName)}

#Template JsonTranslator_Tuple tp:TypeSpec NamespaceName:String
    $$
        var SimpleName = tp.SimpleName(NamespaceName);
        var TypeString = GetTypeString(tp, NamespaceName);
    public static function [[${LowercaseCamelize(SimpleName)}FromJson]](j : Dynamic) : ${TypeString}
    {
        var ja = cast(j, Array<Dynamic>);
        return
        {
            $$
                {
                    int k = 0;
                    foreach (var t in tp.Tuple)
                    {
                        ##
                            [[item${k}]] : [[${LowercaseCamelize(t.SimpleName(NamespaceName))}FromJson]](ja[${k}]),
                        k += 1;
                    }
                }
        };
    }
    public static function [[${LowercaseCamelize(SimpleName)}ToJson]](t : ${TypeString}) : Dynamic
    {
        var ja = new Array<Dynamic>();
        $$
            {
                int k = 0;
                foreach (var t in tp.Tuple)
                {
                    ##
                        ja.push([[${LowercaseCamelize(t.SimpleName(NamespaceName))}ToJson]](t.[[item${k}]]));
                    k += 1;
                }
            }
        return ja;
    }

#Template JsonTranslator_Optional o:TypeSpec NamespaceName:String
    $$
        var SimpleName = o.SimpleName(NamespaceName);
        var TypeString = GetTypeString(o, NamespaceName);
        var ElementSimpleName = o.GenericTypeSpec.ParameterValues.Single().SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(SimpleName)}FromJson]](j : Dynamic) : ${TypeString}
    {
        if (hasField(j, "none"))
        {
            return null;
        }
        if (hasField(j, "some"))
        {
            var v = [[${LowercaseCamelize(ElementSimpleName)}FromJson]](getField(j, "some"));
            return v;
        }
        throw "InvalidOperation";
    }
    public static function [[${LowercaseCamelize(SimpleName)}ToJson]](o : ${TypeString}) : Dynamic
    {
        var j : Dynamic = {};
        if (o == null)
        {
            setField(j, "none", unitToJson({}));
        }
        else
        {
            setField(j, "some", [[${LowercaseCamelize(ElementSimpleName)}ToJson]](o));
        }
        return j;
    }

#Template JsonTranslator_List l:TypeSpec NamespaceName:String
    $$
        var SimpleName = l.SimpleName(NamespaceName);
        var TypeString = GetTypeString(l, NamespaceName);
        var ElementSimpleName = l.GenericTypeSpec.ParameterValues.Single().SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(SimpleName)}FromJson]](j : Dynamic) : ${TypeString}
    {
        var ja = cast(j, Array<Dynamic>);
        var a = new ${TypeString}();
        for (e in ja)
        {
            a.push([[${LowercaseCamelize(ElementSimpleName)}FromJson]](e));
        }
        return a;
    }
    public static function [[${LowercaseCamelize(SimpleName)}ToJson]](c : ${TypeString}) : Dynamic
    {
        var ja = new Array<Dynamic>();
        for (e in c)
        {
            ja.push([[${LowercaseCamelize(ElementSimpleName)}ToJson]](e));
        }
        return ja;
    }

#Template JsonTranslator_Set l:TypeSpec NamespaceName:String
    $$
        var SimpleName = l.SimpleName(NamespaceName);
        var TypeString = GetTypeString(l, NamespaceName);
        var ElementSimpleName = l.GenericTypeSpec.ParameterValues.Single().SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(SimpleName)}FromJson]](j : Dynamic) : ${TypeString}
    {
        var ja = cast(j, Array<Dynamic>);
        var a = new ${TypeString}();
        for (e in ja)
        {
            a.set([[${LowercaseCamelize(ElementSimpleName)}FromJson]](e), {});
        }
        return a;
    }
    public static function [[${LowercaseCamelize(SimpleName)}ToJson]](c : ${TypeString}) : Dynamic
    {
        var ja = new Array<Dynamic>();
        for (e in c.keys())
        {
            ja.push([[${LowercaseCamelize(ElementSimpleName)}ToJson]](e));
        }
        return ja;
    }

#Template JsonTranslator_Map l:TypeSpec NamespaceName:String
    $$
        var gp = l.GenericTypeSpec.ParameterValues;
        if (gp.Count != 2)
        {
            throw new ArgumentException();
        }
        var SimpleName = l.SimpleName(NamespaceName);
        var TypeString = GetTypeString(l, NamespaceName);
        var KeySimpleName = gp[0].SimpleName(NamespaceName);
        var ValueSimpleName = gp[1].SimpleName(NamespaceName);
    public static function [[${LowercaseCamelize(SimpleName)}FromJson]](j : Dynamic) : ${TypeString}
    {
        var ja = cast(j, Array<Dynamic>);
        var a = new ${TypeString}();
        for (e in ja)
        {
            var key = [[${LowercaseCamelize(KeySimpleName)}FromJson]](getField(e, "key"));
            var value = [[${LowercaseCamelize(ValueSimpleName)}FromJson]](getField(e, "value"));
            a.set(key, value);
        }
        return a;
    }
    public static function [[${LowercaseCamelize(SimpleName)}ToJson]](c : ${TypeString}) : Dynamic
    {
        var ja = new Array<Dynamic>();
        for (key in c.keys())
        {
            var value = c.get(key);
            var jp : Dynamic = {};
            setField(jp, "key", [[${LowercaseCamelize(KeySimpleName)}ToJson]](key));
            setField(jp, "value", [[${LowercaseCamelize(ValueSimpleName)}ToJson]](value));
            ja.push(jp);
        }
        return ja;
    }

#Template WrapModule NamespaceName:String Imports:List<String> Contents:IEnumerable<String>
    //==========================================================================
    //
    //  Notice:      This file is automatically generated.
    //               Please don't modify this file.
    //
    //==========================================================================

    $$
        if (NamespaceName != "")
        {
            var n = String.Join(".", NamespaceName.Split('.').Select(NamespacePart => LowercaseCamelize(NamespacePart)));
            ##
                package ${n};

            $End
        }
    import haxe.Json;
    import ${Imports};

    ${Contents}

$End
